package image.hog_person_detector;

import image.hog_person_detector.constants.*;

actor histogram_binning() uint(size=8) angle, uint(size=8) intensity ==> uint(size=16) Histogram :
	uint(size=16) histogram[HIST_LENGTH];
	uint(size=16) i := 0;
	uint(size=8) read_count := 0;
	uint(size=8) out_count := 0;
	uint(size=8) slot := 0;
	uint(size=8) left := 0;
	uint(size=8) low := 0;
	uint(size=8) high := 0;
	
	
	initArray: action ==> 
	do
		read_count := 0;
		out_count := 0;
		i := 0;
		while i < HIST_LENGTH do
			histogram[i] := 0;
			i := i + 1;
		end
		i:= 0;
	end
	
    bin: action angle:[angle], intensity:[intense] ==> 
    guard
    	read_count < 64
    do
  		i := intense;
   		if angle <= 10 then
   			histogram[0] := histogram[0] + i;
   		else 
   			if angle >= 170 then 
   				histogram[8] := histogram[8] + i;
   			else 
	   			angle := angle - 10;
	   			slot := angle / 20;
	   			left := angle mod 20;
	   			
	   			if left = 0 then
	   				histogram[slot] := histogram[slot] + i;
	   			else
	   			
		   			low := ((20 - left) * i) / 20;
		   			high := (left * i) /20;
		   		
		   			if slot > 0 then
		   				histogram[slot] := histogram[slot] + low;
		   			end
		   			if slot < 9 then
		   				histogram[slot + 1] := histogram[slot+1] + high;
		   			end	
	   			end
   			end
   		end
   		read_count := read_count + 1;	
    end
    
    outputArray: action ==> Histogram:[histogram[out_count-1]]
    guard
    	out_count < 9 and read_count >= 64
    do
    	out_count := out_count + 1;
    	
	end
	
	reset: action ==>
	guard
		out_count = 9
	end
	
	schedule fsm init:
		init (initArray) --> read;
		read (bin) --> read;
		read (outputArray) --> read;
		read (reset) --> init;
	end
end